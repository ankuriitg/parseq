ext {
  description = """Set of benchmarks for ParSeq"""
}

dependencies {
  compile project(':parseq-batching')
  compile group: 'org.hdrhistogram', name: 'HdrHistogram', version:'2.1.8'
  compile group: 'org.slf4j', name: 'slf4j-simple', version:'1.7.12'
  compile group: 'com.google.guava', name: 'guava', version: '24.1.1-jre'
}

task fatJar(type: Jar) {
  mustRunAfter ':parseq:jar' // for some reason, gradle can't figure out this transitive dependency
  classifier = 'jar-with-dependencies'
  from { configurations.compile.collect { it.isDirectory()? it : zipTree(it) } }
  with jar
  manifest {
    attributes("Created-By": "Gradle",
        "Version": version,
        "Build-JDK": JavaVersion.current())
    attributes 'Main-Class': 'com.linkedin.parseq.CompletableFuturesPerfLarge'
  }
}

task executeJava(type: JavaExec) {
  main = "com.linkedin.parseq.CompletableFuturesPerfLarge"
  classpath = sourceSets.main.runtimeClasspath
}

artifacts {
  archives fatJar
}
